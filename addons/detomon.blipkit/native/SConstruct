#!/usr/bin/env python
import os
import sys

env = SConscript("vendor/godot-cpp/SConstruct")

# For reference:
# - CCFLAGS are compilation flags shared between C and C++
# - CFLAGS are for C-specific compilation flags
# - CXXFLAGS are for C++-specific compilation flags
# - CPPFLAGS are for pre-processor flags
# - CPPDEFINES are for pre-processor defines
# - LINKFLAGS are for linking flags

env.Append(CPPPATH=["src/", "vendor/BlipKit/src/"])
env.Append(CFLAGS=["-Wno-shift-negative-value"])

sources = Glob("src/*.cpp")
sources += Glob('vendor/BlipKit/src/*.c')

if env["target"] in ["editor", "template_debug"]:
    sources += env.GodotCPPDocData("src/gen/doc_data.gen.cpp", source=Glob("doc/classes/*.xml"))

if env["platform"] == "macos":
    library = env.SharedLibrary(
        "bin/libgdblipkit.{}.{}.framework/libgdblipkit.{}.{}".format(
            env["platform"], env["target"], env["platform"], env["target"]
        ),
        source=sources,
    )
else:
    library = env.SharedLibrary(
        "bin/libgdblipkit{}{}".format(env["suffix"], env["SHLIBSUFFIX"]),
        source=sources,
    )

Default(library)
